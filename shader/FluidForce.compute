#version 460 core

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;


// Smooth Kernel
    uniform float h;

// Particle Data
    uniform float particleMass;
    uniform uint TotalParticleCount;
    uniform float xRange;
    uniform float yRange;

// Visicosity
    uniform float viscosity;

// Surface
    uniform float surfThreshold;
    uniform float surfCoeffi;

// Gravity
    uniform vec3 gravityAcel;

// Mouse Force
    uniform vec2 mouseForce;
    
// Wave Power
    uniform float wavePower;

// Total Time
    uniform float time;




    #define PI 3.1415
    #define hSquare (h * h)
    /* 
        Grad_W_Poly6 -> r 제곱을 변수로 사용
                    -> gradient 결과 상대 particle 을 가리키는 벡터를 가진다
                    -> 이 함수는 크기만 계산
        사용처
            : surface normal filed pointing into the fluid 계산
    */
    float Grad_W_Poly6(float r2) { return (315.0 * (-6.0) / (64.0 * PI * pow(abs(h), 9))) * pow(abs(hSquare - r2), 2); }
    /* 
        Laplace_W_Poly6 -> r 제곱을 사용
        사용처
            : curvature of the surface 계산
    */
    float Laplace_W_Poly6(float r2) { return (315.0 / (64 * PI * pow(abs(h), 9))) * ((-18) * pow(abs(hSquare - r2), 2) + 24 * r2 * (hSquare - r2)); }
    /* 
        Grad_W_Spiky -> r 을 사용
                    -> particle 에서 다른 particle 을 향하는 방향을 가진다 => normalized vector
                    -> 이 함수는 단위벡터를 제외한 크기만 계산
        사용처
            : 압력에 의한 힘 계산
    */
    float Grad_W_Spiky(float r) { return (-45.0 / ( PI * pow(abs(h), 6) )) * (pow(abs(h - r), 2)); }
    /* 
        Laplace_W_Viscosity -> r 사용
        사용처
            : 점성에 의한 힘 계산
    */
    float Laplace_W_Viscosity(float r) { return (45.0 / (PI * pow(abs(h), 6))) * (h - r); }





/*
    여전히 정렬이 보장된 ParticleBuffer
        파티클 ~ 카메라 거리, toCamera 값으로 정렬
        Far -> Near 순서로 정렬

    position;                   position

    velocity;                   velocity

    range                       range
    density                     density
    pressure                    pressure


    -               =>          force               = pressure force + viscosity force (Update_Fluid_Force)
                                                    + Surface Force => 표면이라는 게 확정이 되면
                                                    + extra force                      (Update_Extra_Force)
    neighbor                    neighbor

    -                           surfNormal
    -                           isSurface
 */

struct Particle
{
    vec4 position;

    vec4 velocity;

    uvec2 range;
    float density;
    float pressure;

    vec3 force;
    uint neighbor;

    vec3 surfNormal;
    uint isSurface;
};
layout(std430, binding = 2) buffer ParticleBuffer
{
    Particle pData[];
};





// 주변 Particles 의 영향을 받는 힘을 구하는 함수
void Update_Fluid_Force(uint myId)
{
    // force 초기화
    pData[myId].force = vec3(0.0, 0.0, 0.0);


    vec3 PressureForce    = vec3(0.0, 0.0, 0.0);        // 압력에 의한 힘
    vec3 ViscosityForce   = vec3(0.0, 0.0, 0.0);        // 점성에 의한 힘

    vec3 SurfaceNormal    = vec3(0.0, 0.0, 0.0);        // 표면->유체 를 향하는 벡터
    float SurfaceForceMag = 0.0;                        // 표면장력 크기


    // 탐색한 이웃의 수를 체크, neighbor 에 자기 자신이 포함되어 있으므로 1 로 초기화
    // 힘을 구할 때는, 자기 자신에 대해서는 계산하지 않는다
    uint countCheck = 1;

    // range 범위를 조사, far->near
    for(uint neId = pData[myId].range.x; neId <= pData[myId].range.y; neId++)
    {
        // 자기 자신에 대한 계산은 하지 않는다
        if(neId == myId) continue;


        // 논문의 내용을 그대로 적용 => 상대->나, 방향의 벡터를 사용한다
        vec3 relativePos = pData[myId].position.xyz - pData[neId].position.xyz;
        float rSquare = dot(relativePos, relativePos);

        // 영향력 있는 거리 안이다 + 자기 자신에 대한 연산은 제외한다( normalize 에서 에러 발생 )
        if(rSquare != 0 && rSquare < hSquare)
        {
            // 영향을 주는 파티클을 하나 만났다
            countCheck += 1;

            float rLength = length(relativePos);
            vec3 unitRelativePos = normalize(relativePos);

            /*
                압력에 의한 힘 계산
                => 서로를 밀어내는 힘
                따라서 상대에 의해서, 상대로부터 멀어져야 한다
            */
            PressureForce += (-1) * particleMass * (pData[myId].pressure + pData[neId].pressure) / (2 * pData[neId].density) * Grad_W_Spiky(rLength) * unitRelativePos;

            /* 
                점성에 의한 힘 계산
                서로를 당기는 힘
                내 particle 이 상대 particle 방향으로 이동해야 한다
                현재 입자에서 상대 입자의 속도 방향과 같은 방향을 가진다
            */
            ViscosityForce += viscosity * particleMass * (pData[neId].velocity.xyz - pData[myId].velocity.xyz) / pData[neId].density * Laplace_W_Viscosity(rLength);

            /* 
                Surf Normal
                입자의 바깥-> 유체 내부로 들어가는 벡터
                유체 내부를 향하는 surface normal
             */
            SurfaceNormal += particleMass / pData[neId].density * Grad_W_Poly6(rSquare) * relativePos;

            /* 
                Surface Force Magnitude
                입자에 작용하는 표면 장력의 크기
                표면 장력이 존재한다면, 이 크기를 가진다
                방향은 surface normal 과 같다
             */
            SurfaceForceMag += particleMass / pData[neId].density * Laplace_W_Poly6(rSquare);
        }


        // 혹시 영향을 주는 파티클을 모두 만났으면, 더이상 검색할 필요 없다
        if(countCheck >= pData[myId].neighbor) break;
    }

    // 압력, 점성에 의한 힘 저장
    pData[myId].force = (PressureForce + ViscosityForce);

    // surface data 저장
    pData[myId].surfNormal = SurfaceNormal;
    pData[myId].isSurface = 0;

    // surface normal 이 일정 크기 이상인 경우
    if( length(SurfaceNormal) > surfThreshold )
    {
        // 표면 장력을 더한다
        pData[myId].force += (-1) * surfCoeffi * SurfaceForceMag * normalize(SurfaceNormal);
        pData[myId].isSurface = 1;
    }

}   



// 주변 particle 에 영향을 받지 않는, 외력의 합을 계산한다
void Add_Extra_Force(uint myId)
{
    vec3 ExtraForce = vec3(0, 0, 0);
    float curDensity = pData[myId].density;


    // 중력에 의한 힘
    // 나비에 스톡스 방정식에 의해 ~ 밀도 사용
    ExtraForce += gravityAcel * curDensity;


    // 마우스에 의한 힘
    ExtraForce += vec3(-mouseForce.x, 0, -mouseForce.y) * 1.0;


    // 파도의 의한 힘
    vec3 waveF = vec3(0, sin(pData[myId].position.x / xRange * PI + time), 0);
    waveF *= (1 - pData[myId].position.y / yRange) * wavePower;
    
    
    ExtraForce += waveF;

    // 추가적인 힘을 더해 저장
    pData[myId].force += ExtraForce;
}



void main()
{
    // ssbo 에 접근하는 스레드의 인덱스
    uint index = gl_GlobalInvocationID.x;
    // 스레드 인덱스에 대응하는 particle 데이터가 없으면 종료
    if(index >= TotalParticleCount) return;



    // 1. 주변 파티클들에 영향을 받는 힘을 구하고
    Update_Fluid_Force(index);

    // 2. 주변 파티클들에 영향을 안 받는 힘을 구한다
    Add_Extra_Force(index);
}